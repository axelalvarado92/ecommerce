resource "aws_lambda_function" "general_lambda" {
    function_name = var.function_name
    role = aws_iam_role.lambda_role.arn
    handler = var.handler
    runtime = var.runtime

    filename         = var.filename
    source_code_hash = var.source_code_hash
    memory_size      = var.memory_size
    timeout          = var.timeout
    
    reserved_concurrent_executions = var.reserved_concurrent_executions
    
    environment {
        variables = var.environment_variables
     
        }
    
  }

  resource "aws_iam_role" "lambda_role" {
  name = "${var.function_name}-role"
  
  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Sid    = ""
        Principal = {
          Service = "lambda.amazonaws.com"
        }
      },
    ]
  })

  tags = var.tags
}

### Si la lista de ARNs de tablas DynamoDB tiene al menos un elemento, se crea la policy (count = 1).
### Si la lista está vacía, no se crea la policy (count = 0).
resource "aws_iam_policy" "dynamodb_policy" {
  name = "${var.function_name}-dynamodb-policy"
  count = length(var.dynamodb_table_arns) > 0 ? 1 : 0
  
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "dynamodb:GetItem",
          "dynamodb:PutItem",
          "dynamodb:UpdateItem",
          "dynamodb:DeleteItem",
          "dynamodb:Query",
          "dynamodb:Scan"
        ]
        Resource = var.dynamodb_table_arns
      },
    ]
  })
  
}

resource "aws_iam_policy" "sqs_policy" {
  name = "${var.function_name}-sqs-policy"
  count = length(var.sqs_queue_arns) > 0 ? 1 : 0
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "sqs:SendMessage",
          "sqs:ReceiveMessage",
          "sqs:DeleteMessage",
          "sqs:GetQueueAttributes",
          "sqs:ChangeMessageVisibility"
        ]
        Resource = var.sqs_queue_arns
      },
    ]
  })
  
}

### Como el recurso usa count, Terraform lo trata como una lista.
### Si existe (count = 1), su único elemento está en índice 0.
resource "aws_iam_role_policy_attachment" "dynamodb_attachment" {
  count      = length(var.dynamodb_table_arns) > 0 ? 1 : 0           
  role       = aws_iam_role.lambda_role.name
  policy_arn = aws_iam_policy.dynamodb_policy[0].arn
}

resource "aws_iam_role_policy_attachment" "sqs_attachment" {
  count      = length(var.sqs_queue_arns) > 0 ? 1 : 0
  role       = aws_iam_role.lambda_role.name
  policy_arn = aws_iam_policy.sqs_policy[0].arn
}

### Por cada ARN de policy administrada que se pase al módulo,
### se crea un attachment independiente al rol.
resource "aws_iam_role_policy_attachment" "lambda_logs" {
  for_each   = toset(var.managed_policy_arns)
  role       = aws_iam_role.lambda_role.name
  policy_arn = each.value
}
